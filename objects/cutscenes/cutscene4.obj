// cutscene4 script
// by Alexandre @ 2013-01-07

object ".cutscene4_startup"
{
    requires 0.2.0
    always_active

    // ----- main logic -----
    state "main"
    {
        hide
        let $step=0
        create_child .darkambiance 0 0 darkambiance
        create_child .cutscene_skipper
        change_state next_step
    }

    state next_step
    {
        let $step+=1

        execute !step1 if $step==1
        execute !step2 if $step==2
        execute !step3 if $step==3
        execute !step4 if $step==4
        execute !step5 if $step==5

        if "$step > 5" finish
        change_state do_nothing
    }

    state do_nothing
    {
        //  omg,      .---.
        //  it's   .-|'---'|
        //  11:11 (( |     |
        //  PM     '-|     |
        //  !!!!     '.___.'
    }

    state finish
    {
        change_child_state surge @fadeout
        change_child_state darkambiance @destroy
        change_state wait
    }

    state wait
    {
        on_timeout 3.5 end
    }

    state end
    {
        next_level
    }





    // ----- callbacks -----
    state .cutscene_skipper:on_skip
    {
        let $step=999
        change_child_state textualcutscene @destroy
        change_child_state dlgbox @destroy
        change_state finish
    }

    state .dialogbox:on_destroy
    {
        on_timeout 0.5 next_step
    }

    state .textualcutscene:on_finish
    {
        on_timeout 0.5 next_step
    }

    state .cutscene4.animal:just_appeared
    {
        on_timeout 0.5 next_step
    }

    state .cutscene4.animal:just_disappeared
    {
        on_timeout 2.5 next_step
    }





    // ----- steps of the cutscene -----
    state !step1
    {
        let "$_textualcutscene_text=new_array(2)"
        let "set_array_element($_textualcutscene_text, 0, 420)"
        let "set_array_element($_textualcutscene_text, 1, 421)"
        let "$_textualcutscene_timeout=new_array(2)"
        let "set_array_element($_textualcutscene_timeout, 0, 3)"
        let "set_array_element($_textualcutscene_timeout, 1, 3)"
        create_child .textualcutscene 0 0 textualcutscene
        let "delete_array($_textualcutscene_text)"
        let "delete_array($_textualcutscene_timeout)"
    }

    state !step2
    {
        create_child .cutscene4.surge 0 0 surge
        create_child .cutscene4.animal 0 0 animal
    }

    state !step3
    {
        let "$_dialogbox_text_id = new_array(6)"
        let "set_array_element($_dialogbox_text_id, 0, 422)"
        let "set_array_element($_dialogbox_text_id, 1, 423)"
        let "set_array_element($_dialogbox_text_id, 2, 424)"
        let "set_array_element($_dialogbox_text_id, 3, 425)"
        let "set_array_element($_dialogbox_text_id, 4, 426)"
        let "set_array_element($_dialogbox_text_id, 5, 427)"
        let "$_dialogbox_avatar_id = new_array(6)"
        let "set_array_element($_dialogbox_avatar_id, 0, -1)"
        let "set_array_element($_dialogbox_avatar_id, 1, 1)"
        let "set_array_element($_dialogbox_avatar_id, 2, -1)"
        let "set_array_element($_dialogbox_avatar_id, 3, 1)"
        let "set_array_element($_dialogbox_avatar_id, 4, -1)"
        let "set_array_element($_dialogbox_avatar_id, 5, 1)"
        create_child .dialogbox 0 0 dlgbox
        let "delete_array($_dialogbox_avatar_id)"
        let "delete_array($_dialogbox_text_id)"
    }

    state !step4
    {
        change_child_state animal @go_away
    }

    state !step5
    {
        change_child_state surge @cross_arms

        let "$_dialogbox_text_id = new_array(2)"
        let "set_array_element($_dialogbox_text_id, 0, 428)"
        let "set_array_element($_dialogbox_text_id, 1, 429)"
        let "$_dialogbox_avatar_id = new_array(2)"
        let "set_array_element($_dialogbox_avatar_id, 0, 1)"
        let "set_array_element($_dialogbox_avatar_id, 1, 1)"
        create_child .dialogbox 0 0 dlgbox
        let "delete_array($_dialogbox_avatar_id)"
        let "delete_array($_dialogbox_text_id)"
    }
}


// -------------------

object .cutscene4.animal
{
    requires 0.2.0
    always_active
    detach_from_camera

    state main
    {
        let "$speed = 140"
        let "$min_x = screen_width()/2 + 25"
        let "$floor_y = screen_height()/2 + 20"

        set_animation SD_ANIMAL 19
        set_animation_speed_factor 1.5
        set_absolute_position screen_width() $floor_y
        set_alpha 0
        look_left

        change_state appear
    }

    state appear
    {
        set_alpha "min(1, alpha() + 0.5 * dt())"
        set_absolute_position "max(xpos() - $speed * dt(), $min_x)" $floor_y
        if "xpos() <= $min_x" appeared
    }

    state appeared
    {
        play_sample samples/help.wav
        change_parent_state .cutscene4.animal:just_appeared
        change_state keep_jumping
    }

    state keep_jumping
    {
        set_alpha "min(1, alpha() + 0.5 * dt())"
        set_animation_speed_factor 1.0
        set_absolute_position xpos() "$floor_y - abs(15 * sin(2*pi()*($t+=dt())))"
    }

    state disappear
    {
        set_animation_speed_factor 1.5
        look_right
        set_absolute_position "xpos() + $speed * dt()" "min($floor_y, ypos() + $speed * dt())"
        if "xpos() - hotspot_x() > screen_width()" disappeared
    }

    state disappeared
    {
        change_parent_state .cutscene4.animal:just_disappeared
        destroy
    }

    // callbacks
    state @go_away
    {
        change_state disappear
    }
}



object .cutscene4.surge
{
    requires 0.2.0
    always_active
    detach_from_camera

    state main
    {
        hide

        let "$speed = 140"
        let "$x = screen_width()/2 - 25"
        let "$floor_y = screen_height()/2 + 20"

        set_animation SD_SURGE 34
        set_animation_speed_factor 0
        set_absolute_position $x $floor_y
        look_right

        set_alpha 0
        change_state fadein
    }

    state fadein
    {
        show
        set_alpha "min(1, alpha() + 0.5 * dt())"
        if alpha()>=1 okay
    }

    state okay
    {
        set_animation_speed_factor 1
        on_animation_finished worried
    }

    state worried
    {
        set_animation SD_SURGE 35
    }

    // callbacks
    state @cross_arms
    {
        set_animation SD_SURGE 36
    }

    state @fadeout
    {
        set_alpha "max(0, alpha() - 3*dt())"
    }
}
