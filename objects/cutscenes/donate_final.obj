// "Please donate" screen
// script by Alexandre, 2013-01-07

object ".donate_final_startup"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        create_child ".default_startup.opening_animation" // hihihi ...
        change_state wait
    }

    state wait
    {
        on_timeout 3 done
    }

    state done
    {
        create_child .donate_final_startup.title
        create_child .donate_final_startup.text
        create_child .donate_final_startup.options
        destroy
    }
}

object ".donate_final_startup.title"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position screen_width()/2 10
        change_state loop
    }

    state loop
    {
        elliptical_trajectory 0 2 0 1
        textout_centre menu.title 0 0 $DONATE_TITLE
    }
}

object ".donate_final_startup.text"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position 10 40
        textout menu.small 0 0 $DONATE2_TEXT screen_width()-20
    }
}

object ".donate_final_startup.options"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position screen_width()/2 screen_height()-22
        create_child .donate_final_startup.options.text 0 0 text
        change_state yes
    }

    state no
    {
        change_child_state text no
        on_button_pressed fire1 chose_no
        on_button_pressed fire3 chose_no
        on_button_pressed fire4 chose_no
        on_button_pressed left change_to_yes
        on_button_pressed right change_to_yes
    }

    state yes
    {
        change_child_state text yes
        on_button_pressed fire1 chose_yes
        on_button_pressed fire3 chose_yes
        on_button_pressed fire4 chose_no
        on_button_pressed left change_to_no
        on_button_pressed right change_to_no
    }

    state change_to_no
    {
        play_sample samples/switch.wav
        change_state no
    }

    state change_to_yes
    {
        play_sample samples/switch.wav
        change_state yes
    }

    state chose_yes
    {
        create_child .launch_donation_url

        let $_fade_in=0
        let $_fade_duration=1
        create_child .fade_effect
        let $_fademusic_in=0
        let $_fademusic_duration=1
        create_child .fademusic

        play_sample samples/select.wav
        change_state wait
    }

    state chose_no
    {
        let $_fade_in=0
        let $_fade_duration=1
        create_child .fade_effect
        let $_fademusic_in=0
        let $_fademusic_duration=1
        create_child .fademusic

        play_sample samples/return.wav
        change_state wait
    }

    state wait
    {
        on_timeout 1 bye
    }

    state bye
    {
        next_level
    }
}

object ".donate_final_startup.options.text"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position screen_width()/2 screen_height()-32
        create_child .donate_final_startup.options.text.yes 0 0 yes
        create_child .donate_final_startup.options.text.no 0 0 no
        change_state no
    }

    state yes
    {
        change_child_state yes enabled
        change_child_state no disabled
    }

    state no
    {
        change_child_state no enabled
        change_child_state yes disabled
    }
}

object ".donate_final_startup.options.text.yes"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        look_right
        set_animation SD_HUDHEAD 0
        set_absolute_position screen_width()*0.33 ypos()
        change_state disabled
    }

    state enabled
    {
        show
        textout_centre menu.small 0 10 "<color=$COLOR_MENUSELECTEDOPTION>$DONATE_YES</color>"
    }

    state disabled
    {
        hide
        textout_centre menu.small 0 10 "$DONATE_YES"
    }
}

object ".donate_final_startup.options.text.no"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        look_left
        set_animation SD_HUDHEAD 0
        set_absolute_position screen_width()*0.66 ypos()
        change_state disabled
    }

    state enabled
    {
        show
        textout_centre menu.small 0 10 "<color=$COLOR_MENUSELECTEDOPTION>$DONATE_NO</color>"
    }

    state disabled
    {
        hide
        textout_centre menu.small 0 10 "$DONATE_NO"
    }
}
