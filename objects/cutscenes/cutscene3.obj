// cutscene3 script
// by Alexandre @ 2013-01-06

object ".cutscene3_startup"
{
    requires 0.2.0
    always_active

    // ----- main logic -----
    state "main"
    {
        hide
        let $step=0
        create_child .darkambiance 0 0 darkambiance
        create_child .cutscene_skipper
        change_state next_step
    }

    state next_step
    {
        let $step+=1

        execute !step1 if $step==1
        execute !step2 if $step==2
        execute !step3 if $step==3
        execute !step4 if $step==4
        execute !step5 if $step==5
        execute !step6 if $step==6
        execute !step7 if $step==7
        execute !step8 if $step==8
        execute !step9 if $step==9
        execute !step10 if $step==10
        execute !step11 if $step==11
        execute !step12 if $step==12

        if "$step > 12" finish
        change_state do_nothing
    }

    state do_nothing
    {
        //  omg,      .---.
        //  it's   .-|'---'|
        //  1:11  (( |     |
        //  AM     '-|     |
        //  !!!!     '.___.'
    }

    state finish
    {
        change_child_state darkambiance @destroy
        change_state wait
    }

    state wait
    {
        on_timeout 2 end
    }

    state end
    {
        next_level
    }





    // ----- callbacks -----
    state .cutscene_skipper:on_skip
    {
        let $step=999
        change_child_state textualcutscene @destroy
        change_child_state dlgbox @destroy
        change_state finish
    }

    state .dialogbox:on_destroy
    {
        on_timeout 0.5 next_step
    }

    state .textualcutscene:on_finish
    {
        on_timeout 0.5 next_step
    }





    // ----- steps of the cutscene -----
    state !step1
    {
        let "$_textualcutscene_text=new_array(1)"
        let "set_array_element($_textualcutscene_text, 0, 300)"
        let "$_textualcutscene_timeout=new_array(1)"
        let "set_array_element($_textualcutscene_timeout, 0, 3)"
        create_child .textualcutscene 0 0 textualcutscene
        let "delete_array($_textualcutscene_text)"
        let "delete_array($_textualcutscene_timeout)"
    }

    state !step2
    {
        let $_dialogbox_text_id=301
        let $_dialogbox_avatar_id=1
        create_child .dialogbox 0 0 dlgbox
    }

    state !step3
    {
        let "$_textualcutscene_text=new_array(1)"
        let "set_array_element($_textualcutscene_text, 0, 302)"
        let "$_textualcutscene_timeout=new_array(1)"
        let "set_array_element($_textualcutscene_timeout, 0, 3)"
        create_child .textualcutscene 0 0 textualcutscene
        let "delete_array($_textualcutscene_text)"
        let "delete_array($_textualcutscene_timeout)"
    }

    state !step4
    {
        let $_dialogbox_text_id=303
        let $_dialogbox_avatar_id=1
        create_child .dialogbox 0 0 dlgbox
    }

    state !step5
    {
        let "$_textualcutscene_text=new_array(1)"
        let "set_array_element($_textualcutscene_text, 0, 0)" // empty string
        let "$_textualcutscene_timeout=new_array(1)"
        let "set_array_element($_textualcutscene_timeout, 0, 1)"
        create_child .textualcutscene 0 0 textualcutscene
        let "delete_array($_textualcutscene_text)"
        let "delete_array($_textualcutscene_timeout)"
    }

    state !step6
    {
        let $_dialogbox_text_id=304
        let $_dialogbox_avatar_id=1
        create_child .dialogbox 0 0 dlgbox
    }

    state !step7
    {
        let "$_textualcutscene_text=new_array(2)"
        let "set_array_element($_textualcutscene_text, 0, 0)"
        let "set_array_element($_textualcutscene_text, 1, 305)"
        let "$_textualcutscene_timeout=new_array(2)"
        let "set_array_element($_textualcutscene_timeout, 0, 1)"
        let "set_array_element($_textualcutscene_timeout, 1, 3)"
        create_child .textualcutscene 0 0 textualcutscene
        let "delete_array($_textualcutscene_text)"
        let "delete_array($_textualcutscene_timeout)"
    }

    state !step8
    {
        play_sample samples/help.wav

        let $_dialogbox_text_id=306
        let $_dialogbox_avatar_id=-1
        create_child .dialogbox 0 0 dlgbox
        create_child .cutscene3.younganimal
    }

    state !step9
    {
        let "$_textualcutscene_text=new_array(2)"
        let "set_array_element($_textualcutscene_text, 0, 0)"
        let "set_array_element($_textualcutscene_text, 1, 307)"
        let "$_textualcutscene_timeout=new_array(2)"
        let "set_array_element($_textualcutscene_timeout, 0, 1)"
        let "set_array_element($_textualcutscene_timeout, 1, 3.5)"
        create_child .textualcutscene 0 0 textualcutscene
        let "delete_array($_textualcutscene_text)"
        let "delete_array($_textualcutscene_timeout)"
    }

    state !step10
    {
        play_sample samples/grab_npc.wav

        let "$_textualcutscene_text=new_array(2)"
        let "set_array_element($_textualcutscene_text, 0, 0)"
        let "set_array_element($_textualcutscene_text, 1, 308)"
        let "$_textualcutscene_timeout=new_array(2)"
        let "set_array_element($_textualcutscene_timeout, 0, 1.5)"
        let "set_array_element($_textualcutscene_timeout, 1, 3)"
        create_child .textualcutscene 0 0 textualcutscene
        let "delete_array($_textualcutscene_text)"
        let "delete_array($_textualcutscene_timeout)"
    }

    state !step11
    {
        play_sample samples/death.wav

        let "$_textualcutscene_text=new_array(2)"
        let "set_array_element($_textualcutscene_text, 0, 0)"
        let "set_array_element($_textualcutscene_text, 1, 309)"
        let "$_textualcutscene_timeout=new_array(2)"
        let "set_array_element($_textualcutscene_timeout, 0, 2)"
        let "set_array_element($_textualcutscene_timeout, 1, 4)"
        create_child .textualcutscene 0 0 textualcutscene
        let "delete_array($_textualcutscene_text)"
        let "delete_array($_textualcutscene_timeout)"
    }

    state !step12
    {
        // this may leak out, if the cutscene is skipped at this point D:
        // we need reference counting for the arrays in the engine...
        let "$_dialogbox_text_id = new_array(5)"
        let "set_array_element($_dialogbox_text_id, 0, 310)"
        let "set_array_element($_dialogbox_text_id, 1, 311)"
        let "set_array_element($_dialogbox_text_id, 2, 312)"
        let "set_array_element($_dialogbox_text_id, 3, 313)"
        let "set_array_element($_dialogbox_text_id, 4, 314)"
        let "$_dialogbox_avatar_id = new_array(5)"
        let "set_array_element($_dialogbox_avatar_id, 0, 1)"
        let "set_array_element($_dialogbox_avatar_id, 1, 1)"
        let "set_array_element($_dialogbox_avatar_id, 2, 1)"
        let "set_array_element($_dialogbox_avatar_id, 3, 1)"
        let "set_array_element($_dialogbox_avatar_id, 4, 1)"
        create_child .dialogbox 0 0 dlgbox
        let "delete_array($_dialogbox_avatar_id)"
        let "delete_array($_dialogbox_text_id)"
    }
}


// -------------------

object .cutscene3.younganimal
{
    requires 0.2.0
    always_active
    detach_from_camera

    state main
    {
        set_animation SD_ANIMAL 3
        set_animation_speed_factor 2
        set_scale 2 2
        set_absolute_position -width() screen_height()/2+height()

        create_child .cutscene3.monster -200 -50
        create_child .cutscene3.monster -300 -70
        create_child .cutscene3.younganimal.panic 0 -63

        change_state run
    }

    state run
    {
        move 180 0
    }
}

object .cutscene3.monster
{
    requires 0.2.0
    always_active
    detach_from_camera

    state main
    {
        let "$id = ($_ct3m_id += 1) mod 2"
        execute !create_sfx if $id==0
        set_animation Crococopter 0
        set_scale 0.5 0.5
        change_state run
    }

    state run
    {
        let $speed=215
        set_absolute_position "xpos() + $speed * dt()" "spawnpoint_y() + 10*cos(2*pi()*($t+=dt()) + pi()*($id/2))"
        if "xpos()-hotspot_x() > screen_width()" destroy
    }

    state destroy
    {
        change_child_state sfx @destroy
        destroy
    }

    // ---
    state !create_sfx
    {
        create_child .cutscene3.monster.sfx 0 0 sfx
    }
}

object .cutscene3.monster.sfx
{
    requires 0.2.0
    always_active

    state main
    {
        hide
        let $t=infinity()
        play_sample samples/crococopter_swoop_up.wav 0.5
        change_state w1
    }

    state w1
    {
        on_timeout 0.6 p2
    }

    state p2
    {
        let $t2=0
        play_sample samples/crococopter.wav 0.5
        change_state w2
    }

    state w2
    {
        if ($t-=dt())<=0 p3
        if ($t2+=dt())>=0.85 p2
    }

    state p3
    {
        stop_sample samples/crococopter.wav
        play_sample samples/crococopter_swoop_down.wav 0.5
        change_state w3
    }

    state w3
    {
    }

    // -- callbacks --
    state @destroy
    {
        let $t=1.6
        change_state p2
    }
}


object .cutscene3.younganimal.panic
{
    requires 0.2.0
    always_active
    detach_from_camera

    state main
    {
        set_animation SD_SHOUTPANIC 0
        move 180 0
    }
}
