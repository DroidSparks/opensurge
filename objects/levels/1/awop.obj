// ----------------------------------------
// Open Surge Engine
// http://opensnc.sourceforge.net
//
// Awesome (O)Possum is a character found in Ultimate Smash Friends.
// The sprite was made by Redshrike.
// 
// Changelog:
// 2011-12-23 - first version by Alexandre
// ----------------------------------------

object "Awesome (O)Possum"
{
    requires 0.2.0
    always_active
    category "enemy" "level 1"
    annotation "This character from Ultimate Smash Friends makes a cameo appearance at Sunshine Paradise"

    state "main"
    {
        set_animation "SD_AWOP" 0
        set_zindex 0.4
        on_timeout 1.0+$delta toggle_direction
        set_absolute_position spawnpoint_x()+random(3) ypos() // shake

        on_player_collision talk
    }

    state toggle_direction
    {
        let $delta=random(200)*0.01
        if direction()==1 look_left
        change_state look_right
    }

    state look_left
    {
        look_left
        change_state main
    }

    state look_right
    {
        look_right
        change_state main
    }

    state talk
    {
        let $_dialogbox_text_id=130+(min(($x+=1),2)-1)
        let $_dialogbox_avatar_id=-1
        create_child .dialogbox
        change_state talking
    }

    state talking
    {
        set_absolute_position spawnpoint_x()+random(3) ypos() // shake
    }

    state .dialogbox:on_destroy // called by someone else
    {
        if $x==1 give_rings
        hit_player
        change_state cooldown
    }

    state cooldown
    {
        set_absolute_position spawnpoint_x()+random(3) ypos() // shake
        on_timeout 3.0 main
    }

    state give_rings
    {
        set_absolute_position spawnpoint_x()+random(3) ypos() // shake
        if $rings>=50 main
        on_timeout 3/50 one_more_ring
    }

    state one_more_ring
    {
        let $rings+=1
        add_collectibles 1
        play_sample ring
        return_to_previous_state
    }
}

