// ---------------------------------------------------------------------------
// Open Neon Engine
// http://opensnc.sourceforge.net
//
// File:   default_companions/neon.inc
// Desc:   Neon's companion
// Author: Alexandre
// Date:   2011-02-09, 2011-02-11, 2011-04-23, 2012-02-23
// ---------------------------------------------------------------------------

object ".neon_companion"
{
    requires 0.2.0
    always_active

    state "main"
    {
        create_child ".neon_spindash_controller"
        create_child ".neon_easter"
        destroy
    }
}


object ".neon_spindash_controller"
{
    requires 0.2.0
    always_active

    state "main"
    {
        hide
        observe_player "Neon"
        change_state "stand by"
    }

    state "stand by"
    {
        on_player_duck "duck"
    }

    state "duck"
    {
        let "$p = 0"
        on_button_pressed "fire1" "charge"
        on_player_duck "duck"
        change_state "stand by"
    }

    state "charge"
    {
        set_player_animation "SD_NEON" "6"
        strong_player
        springfy_player // so that the camera won't go down, since the player is ducking
        disable_player_movement
        play_sample "charge"
        let "$p = min($p+2, 8)"
        change_state "hold"
    }

    state "hold"
    {
        //textout menu.small 0 0 $p
        set_player_animation "SD_NEON" "6"
        let "$p -= 0.234 * floor(8*$p) * dt()"

        // check if there's a platform underneath the player
        attach_to_player 0 20
        unless "obstacle_exists(0,0)" "cancel"

        // create cool particles
        let "$_pixelparticle_anim = 1 + random(3)"
        let "$_pixelparticle_xvel = -player_direction() * (120 + random(60))"
        let "$_pixelparticle_yvel = -60 - random(120)"
        create_child .pixelparticle -player_direction()*7 -2
        let "$_pixelparticle_anim = 1 + random(3)"
        let "$_pixelparticle_xvel = -player_direction() * (120 + random(60))"
        let "$_pixelparticle_yvel = -60 - random(120)"
        create_child .pixelparticle -player_direction()*7 -2
        let "$_pixelparticle_anim = 1 + random(3)"
        let "$_pixelparticle_xvel = -player_direction() * (120 + random(60))"
        let "$_pixelparticle_yvel = -60 - random(120)"
        create_child .pixelparticle -player_direction()*7 -2

        // check if the user wants to charge more, or if we can release the player
        on_button_pressed "fire1" "charge"
        on_button_down "down" "hold"
        change_state "release"
    }

    state "cancel"
    {
        enable_player_movement
        weak_player
        change_state "stand by"
    }

    state "release"
    {
        play_sample "release"
        enable_player_movement
        weak_player
        set_player_xspeed "(480 + 30 * floor($p)) * player_direction()"
        roll_player
        change_state "stand by"
    }

}

object ".neon_easter"
{
    requires 0.2.0
    always_active

    state "main"
    {
        hide
        observe_player "Neon"
        change_state "get easter day"
    }

    state "get easter day"
    {
        let "$year = date_year() + 1900"
        let "$n1 = $year mod 19"
        let "$n2 = floor($year / 100)"
        let "$n3 = $year mod 100"
        let "$n4 = floor($n2 / 4)"
        let "$n5 = $n2 mod 4"
        let "$n6 = floor(($n2 + 8) / 25)"
        let "$n7 = floor(($n2 - $n6 + 1) / 3)"
        let "$n8 = (19 * $n1 + $n2 - $n4 - $n7 + 15) mod 30"
        let "$n9 = floor($n3 / 4)"
        let "$n10 = $n3 mod 4"
        let "$n11 = (32 + 2 * $n5 + 2 * $n9 - $n8 - $n10) mod 7"
        let "$n12 = floor(($n1 + 11 * $n8 + 22 * $n11) / 451)"
        let "$month = floor(($n8 + $n11 - 7 * $n12 + 114) / 31)"
        let "$day = 1 + ($n8 + $n11 - 7 * $n12 + 114) mod 31"
        change_state "subtract 6 days"
    }

    state "subtract 6 days"
    {
        let "$c = ($day < 7) and ($month == 4)"
        let "$day_off = cond($c, 25+$day, $day-6)"
        let "$month_off = cond($c, $month-1, $month)"
        change_state "check if easter week"
    }

    state "check if easter week"
    {
        let "$m = 1 + date_mon()"
        let "$d = date_mday()"
        if "($m >= $month_off) and ($m <= $month) and ($d >= $day_off) and ($d <= $day)" "easter wait"
        change_state "end"
    }

    state "end"
    {
        destroy
    }

    state "easter wait"
    {
        on_player_win "easter show"
    }

    state "easter show"
    {
        set_player_animation "SD_EASTERNEON" 0
    }
}

