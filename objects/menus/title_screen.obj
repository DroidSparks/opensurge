//
// Open Surge
// Title screen
//
// Script by Alexandre
// Last update: 2013-02-12
//

object ".title_screen"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide

        // TODO: move these to the main menu
        add_lives -lives()
        add_lives initial_lives()
        add_to_score -score()
        reset_globals

        // creating components
        create_child ".title_screen.message"
        create_child ".title_screen.version"

        // done!
        change_state "idle"
    }

    state "idle"
    {
        on_timeout 1.0 "watch"
    }

    state "watch"
    {
        on_button_pressed "fire3" "done"
    }

    state "done"
    {
        push_quest "quests/options.qst"
        //push_quest "quests/default.qst"
    }
}

object ".title_screen.message"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position screen_width()/2 screen_height()-30
        let "$tmr = 0.5"
        change_state "invisible"
    }

    state "invisible"
    {
        on_timeout $tmr visible
    }

    state "visible"
    {
        textout_centre "menu.main" 0 0 "<color=ffee8e>$TITLE_MESSAGE</color>"
        on_timeout $tmr invisible
    }
}

object ".title_screen.version"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position screen_width()-3 screen_height()-11
        change_state "display"
    }

    state "display"
    {
        textout_right "menu.tiny" 0 0 "$ENGINE_VERSION"
    }
}
