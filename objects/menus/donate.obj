// "Please donate" screen
// script by Alexandre
object ".donate_startup"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        create_child .donate_startup.title
        create_child .donate_startup.text
        create_child .donate_startup.options
        let $_fade_in=1
        let $_fade_duration=1
        create_child .fade_effect
        destroy
    }
}

object ".donate_startup.title"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position screen_width()/2 10
        change_state loop
    }

    state loop
    {
        elliptical_trajectory 0 2 0 1
        textout_centre menu.title 0 0 $DONATE_TITLE
    }
}

object ".donate_startup.text"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position 10 40
        textout menu.small 0 0 $DONATE_TEXT screen_width()-20
    }
}

object ".donate_startup.options"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position screen_width()/2 screen_height()-22
        create_child .donate_startup.options.text 0 0 text
        change_state no
    }

    state yes
    {
        change_child_state text yes
        on_button_pressed fire1 chose_yes
        on_button_pressed fire3 chose_yes
        on_button_pressed fire4 chose_no
        on_button_pressed left change_to_dontask
        on_button_pressed right change_to_no
    }

    state no
    {
        change_child_state text no
        on_button_pressed fire1 chose_no
        on_button_pressed fire3 chose_no
        on_button_pressed fire4 chose_no
        on_button_pressed left change_to_yes
        on_button_pressed right change_to_dontask
    }

    state dontask
    {
        change_child_state text dontask
        on_button_pressed fire1 chose_dontask
        on_button_pressed fire3 chose_dontask
        on_button_pressed fire4 chose_dontask
        on_button_pressed left change_to_no
        on_button_pressed right change_to_yes
    }

    state change_to_no
    {
        play_sample samples/switch.wav
        change_state no
    }

    state change_to_yes
    {
        play_sample samples/switch.wav
        change_state yes
    }

    state change_to_dontask
    {
        play_sample samples/switch.wav
        change_state dontask
    }

    state chose_yes
    {
        create_child .launch_donation_url

        let $_fade_in=0
        let $_fade_duration=1
        create_child .fade_effect

        play_sample samples/select.wav
        change_state wait
    }

    state chose_no
    {
        let $_fade_in=0
        let $_fade_duration=1
        create_child .fade_effect

        play_sample samples/return.wav
        change_state wait
    }

    state chose_dontask
    {
        create_child .donate_startup.dont_show_again

        let $_fade_in=0
        let $_fade_duration=1
        create_child .fade_effect

        play_sample samples/return.wav
        change_state wait
    }

    state wait
    {
        on_timeout 1 bye
    }

    state bye
    {
        let $_force_donation_screen=0
        next_level
    }
}

object ".donate_startup.options.text"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        hide
        set_absolute_position screen_width()/2 screen_height()-32
        create_child .donate_startup.options.text.yes 0 0 yes
        create_child .donate_startup.options.text.no 0 0 no
        create_child .donate_startup.options.text.dontask 0 0 dontask
        change_state no
    }

    state yes
    {
        change_child_state yes enabled
        change_child_state no disabled
        change_child_state dontask disabled
    }

    state no
    {
        change_child_state yes disabled
        change_child_state no enabled
        change_child_state dontask disabled
    }

    state dontask
    {
        change_child_state yes disabled
        change_child_state no disabled
        change_child_state dontask enabled
    }
}

object ".donate_startup.options.text.yes"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        set_animation SD_HUDHEAD 0
        look_right
        set_absolute_position screen_width()/6 ypos()
        change_state disabled
    }

    state enabled
    {
        show
        textout_centre menu.small 0 10 "<color=$COLOR_MENUSELECTEDOPTION>$DONATE_YES</color>"
    }

    state disabled
    {
        hide
        textout_centre menu.small 0 10 "$DONATE_YES"
    }
}

object ".donate_startup.options.text.no"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        set_animation SD_HUDHEAD 0
        look_left
        set_absolute_position screen_width()/2 ypos()
        change_state disabled
    }

    state enabled
    {
        show
        textout_centre menu.small 0 10 "<color=$COLOR_MENUSELECTEDOPTION>$DONATE_NO</color>"
    }

    state disabled
    {
        hide
        textout_centre menu.small 0 10 "$DONATE_NO"
    }
}

object ".donate_startup.options.text.dontask"
{
    requires 0.2.0
    detach_from_camera
    always_active

    state "main"
    {
        set_animation SD_HUDHEAD 0
        look_left
        set_absolute_position screen_width()*5/6 ypos()
        change_state disabled
    }

    state enabled
    {
        show
        textout_centre menu.small 0 10 "<color=$COLOR_MENUSELECTEDOPTION>$DONATE_DONTASK</color>"
    }

    state disabled
    {
        hide
        textout_centre menu.small 0 10 "$DONATE_DONTASK"
    }
}





// ======= prevents this scene from showing up again =======

object ".donate_startup.dont_show_again"
{
    requires 0.2.0
    always_active

    state main
    {
        hide
        create_child .donate_startup.skip_scene 0 0 skip
        change_child_state skip @save_and_destroy

        // warning!
        //destroy // this generates an obscure bug: it will create many copies of this object!
                  // well... wait for an immediate callback from skip_scene instead.
    }

    // --- callbacks ---
    state @destroy
    {
        destroy
    }
}

object ".donate_startup.skip_scene"
{
    requires 0.2.0
    always_active

    state main
    {
        hide
        change_state next
    }

    state next
    {
        change_state next2 // some safety delay... is this really needed?
    }

    state next2
    {
        next_level
    }

    // --- callbacks ---
    state @save_and_destroy
    {
        change_parent_state @destroy // this will execute immediately, before save_level
        save_level
        destroy
    }
}
